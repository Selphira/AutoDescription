BACKUP ~AutoDescription/backup~
AUTHOR ~Selphira (https://www.baldursgateworld.fr/memberlist.php?mode=viewprofile&u=5323)~

VERSION ~0.9.27~

AUTO_EVAL_STRINGS

ALWAYS
	OUTER_PATCH ~~ BEGIN
		PATCH_INCLUDE ~%MOD_FOLDER%/settings-default.ini~
	END

	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/settings.ini~ BEGIN
		OUTER_PATCH ~~ BEGIN
			PATCH_INCLUDE ~%MOD_FOLDER%/settings.ini~
		END
	END

	OUTER_SET is_ee    = (GAME_IS ~bgee bg2ee eet~) ? 1 : 0
	OUTER_SET has_eeex = (MOD_IS_INSTALLED ~EEex.tp2~ ~0~) ? 1 : 0

    ACTION_DEFINE_ARRAY arrayNoConvert BEGIN ~setup~ END
    ACTION_DEFINE_ARRAY arrayReload BEGIN ~description~ END

    LAF HANDLE_CHARSETS
        INT_VAR
            infer_charsets = 1
        STR_VAR
            default_language = ~french~
            tra_path = ~AutoDescription/tra~
            out_path = ~weidu_external/Lang/AutoDescription~
            noconvert_array = arrayNoConvert
            reload_array = arrayReload
    END

    OUTER_SPRINT BGFORGE_LIB_DIR "%MOD_FOLDER%/lib/bgforge"
    INCLUDE ~%BGFORGE_LIB_DIR%/main.tpa~
    INCLUDE ~%MOD_FOLDER%/lib/always.tpa~
END

LANGUAGE ~Francais~ ~french~
         ~AutoDescription/tra/french/setup.tra~
         ~AutoDescription/tra/french/description.tra~

LANGUAGE ~English (Beta)~ ~english~
         ~AutoDescription/tra/english/setup.tra~
         ~AutoDescription/tra/english/description.tra~

BEGIN @10 DESIGNATED 10
SILENT
LAF ~init_log_file~ STR_VAR type = ~items~ mod = ~all~ RET log_filename END
LAM ~init_item_statistics~
LAM ~update_item_descriptions~
LAM ~show_item_statistics~

BEGIN @15 DESIGNATED 15
SILENT
LAF ~init_log_file~ STR_VAR type = ~spells~ mod = ~all~ RET log_filename END
LAM ~init_spell_statistics~
LAM ~update_spell_descriptions~
LAM ~show_spell_statistics~

BEGIN @20 DESIGNATED 20

PRINT @201
ACTION_READLN directory
OUTER_WHILE NOT(DIRECTORY_EXISTS ~%directory%~) BEGIN
	ACTION_IF IS_AN_INT ~%directory%~ AND ~%directory%~ == 0 BEGIN
		ABORT ~~
	END
	PRINT @202
	ACTION_READLN directory
END
ACTION_DEFINE_ARRAY ~itemFiles~ BEGIN END
LAF getItemsFromOtherMod STR_VAR directory = EVAL ~%directory%~ RET_ARRAY itemFiles END
SILENT
LAF ~init_log_file~ STR_VAR type = ~items~ mod = ~%directory%~ RET log_filename END
LAM ~init_item_statistics~
OUTER_SET totalItems = 0
ACTION_PHP_EACH itemFiles AS itemFile => value BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%itemFile%~ BEGIN
		COPY_EXISTING ~%itemFile%~ ~override~
			LPM ~update_item_description~
		BUT_ONLY_IF_IT_CHANGES
	END
END
LAM ~show_item_statistics~